#!/usr/bin/env fift -s

"TonUtil.fif" include

{
  ."Script build internal message BOC for playing in roulette" cr
  ."Arguments: " cr
  ."straight N - Bet on a single number N. N must be any in range 0 to 36 or 00" cr
  ."odd - Bet on any odd number" cr
  ."even - Bet on any even number" cr
  ."red - Bet on any red number" cr
  ."black - Bet on any black number" cr
  ."1st_dozen - Bet on any number in range 1 - 12" cr
  ."2st_dozen - Bet on any number in range 13 - 24" cr
  ."3st_dozen - Bet on any number in range 25 - 36" cr
  ."1to18 - Bet on any number in range 1 - 18" cr
  ."19to36 - Bet on any number in range 19 - 36" cr
  1 halt
} : usage

0 =: op
-2 =: n
now =: queryId

$# 1 = $# 2 = or ' usage ifnot

$1 "straight" $= {
  @' $# 1 = abort"Add number argument for straight bet"
  @' $2 "00" $= {
    -1 =: n
  } {
    @' $2 parse-int =: n
    @' n 0 < @' n 36 > or ' usage if
  } cond
  10 =: op
} if

$1 "odd" $= { 11 =: op } if
$1 "even" $= { 12 =: op } if
$1 "red" $= { 13 =: op } if
$1 "black" $= { 14 =: op } if
$1 "1st_dozen" $= { 15 =: op } if
$1 "2st_dozen" $= { 16 =: op } if
$1 "3st_dozen" $= { 17 =: op } if
$1 "1to18" $= { 18 =: op } if
$1 "19to36" $= { 19 =: op } if

op 0= ' usage if

."op: " op . cr
op 10 = { ."n: " n . cr } if
."query id: " queryId . cr

"play-query.boc" =: bocFile

<b
  op 32 u,
  queryId 64 u,

  op 10 = { n 8 i, } if
b> =: message

// flag 2 - add crc32-c
message 2 boc+>B
bocFile B>file

."New play query saved to " bocFile type cr
